import std::io::{print, println}

glo width: int = 0
glo height: int = 0
glo cells: {[int]: bool} = []

glo fn copy_list(list: {[int]: bool}) -> {[int]: bool} {
    new_list = []
    for i in range(0, len(list)) {
        new_list[i] = list[i]
    }
    return new_list
}

glo fn init(w: int, h: int) {
    glo width = w
    glo height = h
    for i in range(0, width * height) {
        cells[i] = false
    }
}

glo fn get(x: int, y: int) -> bool {
    value = cells[y * width + x]
    if value == null {
        return false
    }
    return value
}

glo fn set(x: int, y: int, value: bool) {
    cells[y * width + x] = value
}

glo fn next_generation(x: int, y: int) -> bool {
    alive_neighbors = 0
    for dx in range(-1, 2) {
        for dy in range(-1, 2) {
            if dx == 0 and dy == 0 {
                continue
            }
            nx = x + dx
            ny = y + dy
            if nx < width and ny < height and get(nx, ny) {
                alive_neighbors += 1
            }
        }
    }

    if get(x, y) and (alive_neighbors == 2 or alive_neighbors == 3) {
        return true
    } else if not get(x, y) and alive_neighbors == 3 {
        return true
    } else {
        return false
    }
}

glo fn next_generation_all() {
    new_cells = copy_list(cells)
    for y in range(0, height) {
        for x in range(0, width) {
            new_cells[y * width + x] = next_generation(x, y)
        }
    }
    glo cells = new_cells
}

glo fn display() {
    for y in range(0, height) {
        for x in range(0, width) {
            if get(x, y) {
                print("██")
            } else {
                print("  ")
            }
        }
        println()
    }
}

init(40, 20)
set(20, 10, true)
set(21, 10, true)
set(22, 10, true)
display()
for i in range(0, 10) {
    println("generation", i)
    next_generation_all()
    display()
}
